（hxzon读书笔记）《clojure编程》第4章：多线程和并发-atom

by hxzon

hxzon学习笔记：

1，修改原子。
swap! 自动不断重试。
compare-and-set! 手工比较来更新。
reset! 强行更新。

2，观察器
add-watch
remove-watch

3，校验器
:validator 在创建atom、ref、agent时指定校验器。
set-validator! 修改4种引用类型的校验器。

=====
1，clojure中有4种引用类型可以用来 表达标识：var，ref，agent和atom。

对引用类型解引用，绝对不会阻塞，也绝不会跟其它操作相互干扰。
这点和delay，future，promise形成鲜明对比。

引用类型除了可以解引用，还可以添加元数据。
但是跟普通值不同，不能使用with-meta或者vary-meta，
对引用类型必须使用alter-meta! ，它会直接修改引用类型的元数据（而不是返回一个新的引用类型）。
atom，ref，agent都接受一个额外的:meta关键字参数，用来设置元数据。


对引用类型可以指定“观察器”，在引用类型状态发生变化时会调用观察器函数，
但不保证值真的变化。

对引用类型还可以指定“校验器”，如果引用类型的新值让校验器函数返回非真，会阻止新值。

2，并发操作的分类

根据同步，非同步，协调，非协调，会产生4种组合。
ref是协调的，同步的。
atom是独立的（所以不需协调），同步的。
agent是不协调的，异步的。

var的主要用途则是线程内对于状态的修改。

clojure的兴趣是解决进城内的并发和并行问题，
所以没有提供协调的、异步的引用类型。
这种类型在分布式系统里面更常见，
例如最终一致性数据库，只保证一段时间后，所有对于状态的修改会合并到最终状态中。

----
两个用来帮助测试的宏。

(defmacro futures
  [n & exprs]
  (vec (for [_ (range n)
             expr exprs]
         `(future ~expr))))

(defmacro wait-futures
  [& args]
  `(doseq [f# (futures ~@args)]
     @f#))
始终返回nil，并且阻塞repl直到所有的future都实例化了。

========
3，atom

atom实现同步的、无须协调的、原子的“先比较再设值”的修改策略。
对于atom的修改操作都要阻塞直到这个修改完成。
每个修改操作都是完全隔离的（自动完全隔离），没有办法协调对两个atom的修改。

(def sarah (atom {:name "Sarah" :age 25 :wears-glasses? false}))
;= #'user/sarah

----
3.1，swap! ，用函数返回值来修改atom。swap! 返回atom的更新值。

(swap! sarah update-in [:age] + 3) 
;= {:age 28, :wears-glasses? false, :name "Sarah"}

(swap! sarah (comp #(update-in % [:age] inc)
               #(assoc % :wears-glasses? true)))
;= {:age 29, :wears-glasses? true, :name "Sarah"}

--
swap! 的语义是先比较旧值，再设值新值，如果atom的值在更新函数返回之前发生了变化，
swap! 会自动重试（以atom的最新值），会一直重试，直到设值成功。
没有办法对swap! 的重试语义进行任何控制，所以更新函数必须是纯的。

(def xs (atom #{1 2 3}))
;= #'user/xs

(wait-futures 1 (swap! xs (fn [v]
                            (Thread/sleep 250)
                            (println "trying 4")
                            (conj v 4)))
  (swap! xs (fn [v]
              (Thread/sleep 500)
              (println "trying 5")
              (conj v 5))))
;= nil
; trying 4
; trying 5 
; trying 5

@xs
;= #{1 2 3 4 5}

(def x (atom 2000))
;= #'user/x

(swap! x #(Thread/sleep %))
;= nil

----
3.2，compare-and-set! ，在你知道atom的当前值下，可以使用。

(compare-and-set! xs :wrong "new value")
;= false

(compare-and-set! xs @xs "new value")
;= true

@xs
;= "new value"

--
compare-and-set! 不适用值语义，它要求你的第2个参数，跟atom的值是identical的。

(def xs (atom #{1 2}))
;= #'user/xs

(compare-and-set! xs #{1 2} "new value")
;= false

----
3.3，reset! ，强行更新。

(reset! xs :y)
;= :y

@xs
;= :y


=======
引用类型共同的特性：观察器和校验器。

4，观察器

add-watch 添加观察器，参数：atom，观察器的名字，观察器。
remove-watch 移除观察器，第2个参数是观察器的名字。

观察器在引用类型的每次修改时都会被调用，但它不保证调用时，引用的值确实发生了变化。
所以需要在观察器里，比较新旧值，看是否真的发生了变化。
观察器的参数：观察器的名字，atom，旧值，新值。

(defn echo-watch
  [key identity old new]
  (println key old "=>" new))
;= #'user/echo-watch

(def sarah (atom {:name "Sarah" :age 25}))
;= #'user/sarah

(add-watch sarah :echo echo-watch)
;= #<Atom@418bbf55: {:name "Sarah", :age 25}>

(swap! sarah update-in [:age] inc)
; :echo {:name Sarah, :age 25} => {:name Sarah, :age 26}
;= {:name "Sarah", :age 26}

(add-watch sarah :echo2 echo-watch)
;= #<Atom@418bbf55: {:name "Sarah", :age 26}>

(swap! sarah update-in [:age] inc)
; :echo {:name Sarah, :age 26} => {:name Sarah, :age 27}
; :echo2 {:name Sarah, :age 26} => {:name Sarah, :age 27}
;= {:name "Sarah", :age 27}

(remove-watch sarah :echo2)
;= #<Atom@418bbf55: {:name "Sarah", :age 27}>

(swap! sarah update-in [:age] inc)
; :echo {:name Sarah, :age 27} => {:name Sarah, :age 28}
;= {:name "Sarah", :age 28}

(reset! sarah @sarah)
; :echo {:name Sarah, :age 28} => {:name Sarah, :age 28}
;= {:name "Sarah", :age 28}

--
示例：利用观察器来记录一个引用的所有历史状态。

(def history (atom ()))

(defn log->list
  [dest-atom key source old new]
  (when (not= old new)
    (swap! dest-atom conj new)))

(def sarah (atom {:name "Sarah", :age 25}))
;= #'user/sarah

(add-watch sarah :record (partial log->list history))
;= #<Atom@5143f787: {:age 25, :name "Sarah"}>

(swap! sarah update-in [:age] inc)
;= {:age 26, :name "Sarah"}

(swap! sarah update-in [:age] inc)
;= {:age 27, :name "Sarah"}

(swap! sarah identity)  ;identity函数总是返回传入的参数。
;= {:age 27, :name "Sarah"}

(swap! sarah assoc :wears-glasses? true)
;= {:age 27, :wears-glasses? true, :name "Sarah"}

(swap! sarah update-in [:age] inc)
;= {:age 28, :wears-glasses? true, :name "Sarah"}

(pprint @history)
;= ;= nil
;= ; ({:age 28, :wears-glasses? true, :name "Sarah"}
;= ; {:age 27, :wears-glasses? true, :name "Sarah"}
;= ; {:age 27, :name "Sarah"}
;= ; {:age 26, :name "Sarah"})

(defn log->db
  [db-id identity old new]
  (when (not= old new)
    (let [db-connection (get-connection db-id)]
      ...)))

(add-watch sarah "jdbc:postgresql://hostname/some_database" log->db)

=====
5，校验器

检验器函数检查新值，通过返回逻辑假，或抛出异常，来使修改失败。

atom、ref、agent可以在创建时，通过:validator关键字来指定校验器。
后期则可通过set-validator! 来改变校验器。

校验器对于ref有着更重要的作用。

(def n (atom 1 :validator pos?))
;= #'user/n

(swap! n + 500)
;= 501

(swap! n - 1000)
;= #<IllegalStateException java.lang.IllegalStateException: Invalid reference state>

--
(def sarah (atom {:name "Sarah" :age 25}))
;= #'user/sarah

(set-validator! sarah :age)
;= nil

(swap! sarah dissoc :age)
;= #<IllegalStateException java.lang.IllegalStateException: Invalid reference state>

--
;如果新值不含有:age关键字，则抛出异常。
(set-validator! sarah #(or (:age %)
                         (throw (IllegalStateException. "People must have `:age`s!"))))
;= nil

(swap! sarah dissoc :age)
;= #<IllegalStateException java.lang.IllegalStateException: People must have `:age`s!>

----
可以选择Slingshot这样的库来抛出一个值，而不是把有用的信息编进一个字符串里。

https://github.com/scgilardi/slingshot

